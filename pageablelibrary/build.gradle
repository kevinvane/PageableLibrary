apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 30



    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 10
        versionName "2.0.0"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    android.libraryVariants.all {
        variant ->
            variant.outputs.all {
                output -> output.outputFileName = "pageablelibrary-release-v" + defaultConfig.versionName + ".aar"
            }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}


// 项目引用的版本号，比如 implementation 'com.kevinvane:demo:1.0.1'
group = "com.kevinwen"
version = "1.0.6"

def siteUrl = 'https://github.com/kevinvane/PageableLibrary' // 项目主页。
def gitUrl = 'git@github.com:kevinvane/PageableLibrary.git' // Git仓库的url。

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                //项目描述
                name 'Pageable Library For Android'// 可选，项目名称。
                description 'The Android Pageable Library.'// 可选，项目描述。
                url siteUrl

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                //填写开发者基本信息，复制我的，这里需要修改。
                developers {
                    developer {
                        id 'kevinenjoy ' // 开发者的id。
                        name 'Sam' // 开发者名字。
                        email 'kevinwenwork@qq.com' // 开发者邮箱。
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false // 忽略注释语法错误，如果用jdk1.8你的注释写的不规范就编译不过。
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 这里是读取Bintray相关的信息，我们上传项目到github上的时候会把gradle文件传上去，所以不要把帐号密码的信息直接写在这里，写在local.properties中，这里动态读取。
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user") // Bintray的用户名。
    key = properties.getProperty("bintray.apikey") // Bintray刚才保存的ApiKey。

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "PageableLibrary"// 发布到Bintray上的项目名字，不是引用的名称
        userOrg = user
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}